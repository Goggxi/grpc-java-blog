plugins {
    id 'com.google.protobuf' version '0.8.16'
    id 'java'
    id 'idea'
}

group 'com.goggxi'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

    // gRPC
    implementation 'io.grpc:grpc-netty-shaded:1.39.0' // shaded include ssl library
    implementation 'io.grpc:grpc-protobuf:1.39.0'
    implementation 'io.grpc:grpc-stub:1.39.0'
    implementation 'io.grpc:grpc-services:1.39.0' // reflection

    // mongo client
    implementation group: 'org.mongodb', name: 'mongodb-driver-sync', version: '4.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.17.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.39.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// if you have source imports issues, add the below
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')
idea {
    module {
        // Marks the already(!) added srcDir as "generated"
        generatedSourceDirs += file('build/generated/source')
    }
}

test {
    useJUnitPlatform()
}